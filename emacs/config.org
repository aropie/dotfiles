* Startup optimizations
** Cache
Many modes store their cache files in =~/.emacs.d=. I prefer to keep
those files in =~/.cache/emacs=
#+BEGIN_SRC emacs-lisp
  (setq user-emacs-directory "~/.cache/emacs/")
  (if (not (file-directory-p user-emacs-directory))
      (make-directory user-emacs-directory t))
#+END_SRC
** Package initialize
#+BEGIN_SRC emacs-lisp
  (require 'package)
  (setq package-archives '(("melpa" . "https://melpa.org/packages/")
                           ("gnu" . "https://elpa.gnu.org/packages/")
                           ("org" . "https://orgmode.org/elpa/")))
  (package-initialize)
#+END_SRC
** Use-package
#+BEGIN_SRC emacs-lisp
  (unless (package-installed-p 'use-package)
    (package-refresh-contents)
    (package-install 'use-package))

  (eval-when-compile
    (require 'use-package))
  (setq use-package-always-ensure t)
#+END_SRC
** Update packages automatically
#+BEGIN_SRC emacs-lisp
  (use-package auto-package-update
    :custom
    (auto-package-update-interval 14)
    (auto-package-update-prompt-before-update t)
    (auto-package-update-hide-results nil)
    :config
    (auto-package-update-maybe)
    (auto-package-update-at-time "09:00"))
#+END_SRC
** Garbage collection
#+BEGIN_SRC emacs-lisp
  (use-package gcmh
    :custom
    (gcmh-idle-delay 0.5)
    (gcmh-high-cons-threshold (* 16 1024 1024))  ; 16mb
    :init
    (gcmh-mode 1))
#+END_SRC
* General layout and config
** Keeping emacs clean
*** No littering
#+BEGIN_SRC emacs-lisp
  (use-package no-littering)
#+END_SRC
*** Auto-saves
#+BEGIN_SRC emacs-lisp
  (setq auto-save-file-name-transforms
        `((".*" ,(no-littering-expand-var-file-name "auto-save/") t)))
#+END_SRC

*** Backups
#+BEGIN_SRC emacs-lisp
  (setq backup-directory-alist `(("." . ,(no-littering-expand-var-file-name "backups/")))
        backup-by-copying t
        delete-old-versions t
        kept-new-versions 6
        kept-old-versions 2
        version-control t)
#+END_SRC

*** Custom
#+BEGIN_SRC emacs-lisp
  (setq custom-file (no-littering-expand-etc-file-name "custom.el"))
#+END_SRC

** Use spaces instead of tabs
#+BEGIN_SRC emacs-lisp
  (setq-default indent-tabs-mode nil)
#+END_SRC
** Set default tab size
#+BEGIN_SRC emacs-lisp
  (setq-default tab-width 4)
#+END_SRC
** Enable narrow mode
#+BEGIN_SRC emacs-lisp
  (put 'narrow-to-region 'disabled nil)
#+END_SRC
** Disable menu, toolbar and scroll
They are visual clutter once you get to know the keybindings (or create your own)
#+BEGIN_SRC emacs-lisp
  (tool-bar-mode -1)
  (menu-bar-mode -1)
  (scroll-bar-mode -1)
#+END_SRC
** Defuse custom file
I really dislike ~custom~ writing on ~init.el~ without my permission,
so I disable it altogether
#+BEGIN_SRC emacs-lisp
  (setq custom-file null-device)
#+END_SRC
** Mac fixes
*** Mac alt fix
#+BEGIN_SRC emacs-lisp
  (setq mac-command-modifier 'meta)
  (setq mac-option-modifier nil)
#+END_SRC
*** Mac path fix
MacOs has problems with paths, so we need to add them manually
#+BEGIN_SRC emacs-lisp
  (use-package exec-path-from-shell
    :if (memq system-type '(darwin windows-nt))
    :config
    (exec-path-from-shell-initialize)
    (exec-path-from-shell-copy-env "GOPATH"))
#+END_SRC
** Scroll conservatively
Makes Emacs scroll more smoothly
#+BEGIN_SRC emacs-lisp
  (setq scroll-conservatively 100)
#+END_SRC
** Disable ring alert
I haven't actually experienced the ring alert, but people often say it's quite
annoying, so better be safe than sorry
#+BEGIN_SRC emacs-lisp
  (setq ring-bell-function 'ignore)
#+END_SRC
** Startup message
I prefer the scratch buffer to greet me when I (rarely) restart Emacs
#+BEGIN_SRC emacs-lisp
  (setq inhibit-startup-message t)
#+END_SRC
** Transparency
I like being able to see my wallpaper, even if it's only a little bit. Call me a romantic if you like
#+BEGIN_SRC emacs-lisp
  (set-frame-parameter (selected-frame) 'alpha '(98 98))
  (add-to-list 'default-frame-alist '(alpha 98 98))
#+END_SRC
** Disable cursors in other windows
I find confusing having cursors on every window, so I just remove inactive ones
#+BEGIN_SRC emacs-lisp
  (setq cursor-in-non-selected-windows nil)
#+END_SRC
** Help
When opening a help window, always select that window afterwards and move focus to it.
#+BEGIN_SRC emacs-lisp
  (setq help-window-select t)
#+END_SRC
** Font
This is supposed to adjust font sizes depending on which screen I'm
working on, but I have yet to find a sweet spot for it
#+BEGIN_SRC emacs-lisp
  (if (eq system-type 'darwin)
      (set-face-attribute 'default nil :height 130)
    (set-face-attribute 'default nil :height 100))
#+END_SRC
** Yes-or-no
Change "yes or no" prompts to "y or n" because we're fast bois
#+BEGIN_SRC emacs-lisp
  (fset 'yes-or-no-p 'y-or-n-p)
#+END_SRC
** Auto revert everything
Since I run a lot of automatic linting, I like for all buffers to match the actual file as closely as possible
#+BEGIN_SRC emacs-lisp
  (global-auto-revert-mode t)
  (setq global-auto-revert-non-file-buffers t)
#+END_SRC
** Correctly set exec-path
#+BEGIN_SRC emacs-lisp
  (defun set-exec-path-from-shell-PATH ()
    "Set up Emacs' `exec-path' and PATH environment variable to match
  that used by the user's shell.

  This is particularly useful under Mac OS X and macOS, where GUI
  apps are not started from a shell."
    (interactive)
    (let ((path-from-shell (replace-regexp-in-string
                            "[ \t\n]*$" "" (shell-command-to-string
                                            "$SHELL --login -c 'echo $PATH'"
                                            ))))
      (setenv "PATH" path-from-shell)
      (setq exec-path (split-string path-from-shell path-separator))))

  (set-exec-path-from-shell-PATH)
#+END_SRC
**
** Always follow symlinks
#+BEGIN_SRC emacs-lisp
  (setq vc-follow-symlinks t)
#+END_SRC
* Utils
** Hydra
#+BEGIN_SRC emacs-lisp
  (use-package hydra)
#+END_SRC
** General.el
Manage all the keybindings through ~general~ and ~hydra~ à la ~spacemacs~
#+BEGIN_SRC emacs-lisp
  (use-package general
    :config
    (general-create-definer my-leader
      ;; :prefix my-leader
      :states '(normal insert emacs)
      :keymaps 'override
      :prefix "SPC"
      :non-normal-prefix "M-SPC")
    (general-create-definer my-local-leader
      ;; prefix local-leader
      :states '(normal insert emacs)
      :prefix "SPC m"
      :non-normal-prefix "M-SPC m"))
#+END_SRC
** Main keybindings
*** File keybindings
#+BEGIN_SRC emacs-lisp
  (my-leader
    :infix "f"
    "" '(:ignore t :which-key "File")
    "f" '(find-file :which-key "Find file")
    "s" '(save-buffer :which-key "Save file")
    "u" '(:ignore t :which-key "Sudo find file (TBD)")
    "U" '(:ignore t :which-key "Sudo this file (TBD)")
    "R" '(:ignore t :which-key "Rename/move this file (TBD)"))
  (my-leader
    "SPC" '(projectile-find-file :which-key "Find file in project")
    "." '(find-file :which-key "Find file")
    "," '(switch-to-buffer :which-key "Switch to buffer"))
#+END_SRC
*** Buffer keybindings
#+BEGIN_SRC emacs-lisp
  (my-leader
    :infix "b"
    "" '(:ignore t :which-key "Buffer")
    "b" '(persp-counsel-switch-buffer :which-key "Switch to workspace buffer")
    "B" '(switch-to-buffer :which-key "Switch to buffer")
    "i" '(ibuffer :which-key "ibuffer")
    "k" '(kill-this-buffer :which-key "Kill buffer")
    "r" '(revert-buffer :which-key "Revert buffer")
    "n" '(next-buffer :which-key "Next buffer")
    "p" '(previous-buffer :which-key "Previous buffer")
    "e" '(set-buffer-file-coding-system :which-key "Set buffer coding system"))
  (my-leader
    "," '(persp-counsel-switch-buffer :which-key "Switch to workspace buffer"))
#+END_SRC
*** Toggle keybindings
#+BEGIN_SRC emacs-lisp
  (my-leader
    :infix "t"
    "" '(:ignore t :which-key "Toggle")
    "l" '(global-linum-mode :which-key "Line numbers")
    "r" '(read-only-mode :which-key "Read only mode")
    "w" '(whitespace-mode :which-key "Whitespace mode"))
#+END_SRC
*** Open keybindings
#+BEGIN_SRC emacs-lisp
  (my-leader
    :infix "o"
    "" '(:ignore t :which-key "Open"))
#+END_SRC
*** Help keybindings
#+BEGIN_SRC emacs-lisp
  (my-leader
    :infix "h"
    "" '(:ignore t :which-key "Help")
    "a" '(apropos-command :which-key "Apropos")
    "k" '(describe-key :which-key "Key")
    "f" '(describe-function :which-key "Function")
    "m" '(describe-mode :which-key "Mode")
    "b" '(describe-bindings :which-key "Bindings")
    "v" '(describe-variable :which-key "Variable"))
#+END_SRC
*** Config shortcuts
I tend to modify a lot my config files, so I set shortcuts to the
most used ones
**** Definitions
#+BEGIN_SRC emacs-lisp
  (defun aropie/emacs-config-visit ()
    (interactive)
    (find-file "~/.emacs.d/config.org"))
  (defun aropie/i3-config-visit ()
    (interactive)
    (find-file "~/.config/i3/config"))
  (defun aropie/keybindings-config-visit ()
    (interactive)
    (find-file "~/.config/sxhkd/sxhkdrc"))
  (defun aropie/zsh-config-visit ()
    (interactive)
    (find-file "~/.zshrc"))
  (defun aropie/xinit-config-visit ()
    (interactive)
    (find-file "~/.xinitrc"))
  (defun aropie/emacs-config-reload ()
    (interactive)
    (org-babel-load-file (expand-file-name "~/.emacs.d/config.org")))
#+END_SRC
**** Bindings
#+BEGIN_SRC emacs-lisp
  (my-leader
    :infix "oc"
    "" '(:ignore t :which-key "Config")
    "e" '(aropie/emacs-config-visit :which-key "emacs")
    "i" '(aropie/i3-config-visit :which-key "i3")
    "z" '(aropie/zsh-config-visit :which-key "zsh")
    "k" '(aropie/keybindings-config-visit :which-key "keybindings")
    "x" '(aropie/xinit-config-visit :which-key "xinitrc")
    "r" '(aropie/emacs-config-reload :which-key "Reload emacs config"))
#+END_SRC
** Scratch buffer
*** Create/call scratch buffer
Utility function to get *scratch* buffer or create it if it was killed
#+BEGIN_SRC emacs-lisp
  (defun aropie/get-scratch-buffer nil
    "create a scratch buffer"
    (interactive)
    (switch-to-buffer (get-buffer-create "*scratch*"))
    (org-mode))
  (my-leader
    :infix "o"
    "s" `(,(if (and (boundp 'persp-mode) (persp-mode))
               'persp-switch-to-scratch-buffer
             'aropie/get-scratch-buffer)
          :which-key "Scratch"))
#+END_SRC
** Which-key
Because Emacs is hard enough without visual aids
#+BEGIN_SRC emacs-lisp
  (use-package which-key
    :init
    (which-key-mode)
    :custom
    (setq which-key-idle-delay 1))
#+END_SRC
** Rg
Ripgrep
#+BEGIN_SRC emacs-lisp
  (use-package rg)
#+END_SRC
** Ivy
#+BEGIN_SRC emacs-lisp
  (use-package ivy
    :custom
    (ivy-wrap t)
    (ivy-height 15)
    (ivy-re-builders-alist ((t . ivy--regex-ignore-order)))
    (ivy-magic-slash-non-match-action 'ivy-magic-slash-non-match-create)
    :config
    (general-define-key
     :keymaps '(ivy-minibuffer-map ivy-switch-buffer-map)
     "C-j" 'ivy-next-line
     "C-k" 'ivy-previous-line
     "C-l" 'ivy-alt-done
     "C-o" 'ivy-dispatching-done
     "C-O" 'ivy-occur
     "C-SPC" 'ivy-call
     "M-RET" 'ivy-immediate-done)
    (ivy-mode 1))
#+END_SRC
*** Ivy-rich
#+BEGIN_SRC emacs-lisp
  (use-package ivy-rich
    :config
    (setcdr (assq t ivy-format-functions-alist) #'ivy-format-function-line)
    (ivy-rich-mode t))
#+END_SRC
*** Ivy-xref
#+BEGIN_SRC emacs-lisp
  (use-package ivy-xref
    :custom
    (setq xref-show-definitions-function #'ivy-xref-show-defs)
    (setq xref-show-xrefs-function #'ivy-xref-show-xrefs))
#+END_SRC
*** Prescient
Add sorting by recent and enhances fuzzy searching of Ivy
#+BEGIN_SRC emacs-lisp
  (use-package ivy-prescient
    :custom (ivy-prescient-retain-classic-highlighting t)
    :init (ivy-prescient-mode t))
#+END_SRC

** Counsel
#+BEGIN_SRC emacs-lisp
  (use-package counsel
    :config
    (counsel-mode t)
    (general-define-key
     "M-y" 'counsel-yank-pop))
#+END_SRC
** Swiper
#+BEGIN_SRC emacs-lisp
  (use-package swiper
    :config
    (general-define-key
     "C-s" 'counsel-grep-or-swiper))
#+END_SRC
** Presentation mode
A mode to enbiggen font for presentations and screen sharing
#+BEGIN_SRC emacs-lisp
  (use-package presentation
    :config
    (defun aropie/presentation-on ()
      (lsp-ui-mode -1))
    (defun aropie/presentation-off ()
      (lsp-ui-mode 1))

    (add-hook 'presentation-on-hook #'aropie/presentation-on)
    (add-hook 'presentation-off-hook #'aropie/presentation-off)

    (my-leader
      :infix "t"
      "p" '(presentation-mode :which-key "Presentation mode")))
#+END_SRC
** Try
Sometimes I want to try a specific package before actually installing it. This is exactly it.
#+BEGIN_SRC emacs-lisp
  (use-package try)
#+END_SRC

** Projectile
Projectile's really cool. Very nice project management.
#+BEGIN_SRC emacs-lisp
  (use-package projectile
    :ensure t
    :custom
    (projectile-indexing-method 'alien)
    (projectile-enable-caching t)
    (projectile-completion-system 'ivy)
    :config
    (add-to-list 'projectile-globally-ignored-directories ".venv")
    (projectile-mode t)
    (my-leader
      :infix "p"
      "" '(:ignore t :which-key "Project")
      "f" '(projectile-find-file :which-key "Find file")
      "F" '(projectile-find-file-other-window :which-key "Find file (other window)")
      "b" '(projectile-switch-to-buffer :which-key "Switch to buffer")
      "B" '(projectile-switch-to-buffer-other-window :which-key "Switch to buffer (other window)")
      "k" '(projectile-kill-buffers :which-key "Kill all project buffers")
      "p" '(projectile-switch-project :which-key "Switch to project")
      "t" '(projectile-toggle-between-implementation-and-test :which-key "Toggle between test and implementation")
      "T" '(projectile-test-project :which-key "Run project's tests")
      "a" '(projectile-add-known-project :which-key "Add bookmark to project")
      "r" '(projectile-replace :which-key "Replace in project")
      "c" '(projectile-invalidate-cache :which-key "Clear project's cache")
      "s" '(counsel-projectile-rg :which-key "Search in project")))
#+END_SRC
*** Counsel-projectile
#+BEGIN_SRC emacs-lisp
  (use-package counsel-projectile
    :after (projectile counsel)
    :config
    (counsel-projectile-mode t))
#+END_SRC
** Dumb-jump
Jumping to definitions made simple
#+BEGIN_SRC emacs-lisp
  (use-package dumb-jump
    :custom
    (dumb-jump-use-visible-window nil)
    :config
    (add-hook 'xref-backend-functions #'dumb-jump-xref-activate)
    (setq dumb-jump-force-searcher 'rg)
    (my-leader
      :infix "d"
      "" '(:ignore t :which-key "Definition")
      "j" '(dumb-jump-go :which-key "Jump to definition")
      "o" '(dumb-jump-go-other-window :which-key "Jump to definition on the other window")
      "l" '(dumb-jump-quick-look :which-key "Look at definition on tooltip")
      "b" '(dumb-jump-back :which-key "Jump back to previous-to-jump position")))
#+END_SRC

** Helpful
Improves ~help~ buffers adding contextual information and multiple references
#+BEGIN_SRC emacs-lisp
  (use-package helpful
    :custom
    (counsel-describe-function-function #'helpful-callable)
    (counsel-describe-variable-function #'helpful-variable)
    :bind
    ([remap describe-function] . counsel-describe-function)
    ([remap describe-command] . helpful-command)
    ([remap describe-variable] . counsel-describe-variable)
    ([remap describe-key] . helpful-key))
#+END_SRC
** Undo tree
Undoing becomes actually a branching tree, one that I can actually navigate. This is some serious voodoo stuff
#+BEGIN_SRC emacs-lisp
  (use-package undo-tree
    :config
    (global-undo-tree-mode t)
    (my-leader
      :infix "o"
      "u" '(undo-tree-visualize :which-key "Undo tree")))
#+END_SRC
** Avy
Jump around like there's no tomorrow
#+BEGIN_SRC emacs-lisp
  (use-package avy
    :custom
    (avy-background t)
    (avy-all-windows t)
    :config
    (defun aropie/avy-jump-to-char-in-one-window()
      (interactive)
      (setq current-prefix-arg '(4)) ; C-u
      (call-interactively 'avy-goto-char-2))

    (general-define-key
     :keymaps 'override
     "C-;" 'aropie/avy-jump-to-char-in-one-window)

    (my-leader
      :infix "j"
      "" '(:ignore t :which-key "Jump")
      "w" '(avy-goto-subword-1 :which-key "Jump to word")
      "l" '(avy-goto-line :which-key "Jump to line")
      "c" '(avy-goto-char :which-key "Jump to char")
      "m" '(:ignore t :which-key "Move...")
      "ml" '(avy-move-line :which-key "Move line")
      "mr" '(avy-move-region :which-key "Move region")
      "y" '(:ignore t :which-key "Yank...")
      "yl" '(avy-copy-line :which-key "Yank line")
      "yr" '(avy-copy-region :which-key "Yank region")
      "k" '(:ignore t :which-key "Kill...")
      "kr" '(avy-kill-region :which-key "Kill region between lines")
      "kl" '(avy-kill-whole-line :which-key "Kill line")))
#+END_SRC
** Ace window
Window managing made smart
#+BEGIN_SRC emacs-lisp
  (use-package ace-window
    :ensure t
    :custom
    (aw-scope 'frame)
    (aw-keys '(?a ?s ?d ?f ?g ?h ?j ?k ?l))
    :config
    (general-define-key
     :states '(normal insert emacs)
     :keymaps 'override
     "C-w C-w" 'ace-window)

    (my-leader
      :infix "w"
      "" '(:ignore t :which-key "Windows")
      "w" '(ace-window :which-key "Change window")
      "s" '(ace-swap-window :which-key "Swap windows")
      "o" '(delete-other-windows :which-key "Delete other windows")
      "x" '(ace-delete-window :which-key "Delete window")
      "h" '(split-window-vertically :which-key "Split window horizontally")
      "v" '(split-window-horizontally :which-key "Split window vertically")
      "r" '(hydra-window-resize/body :which-key "Resize windows")))
#+END_SRC
** Verb
Like [[https://github.com/pashky/restclient.el][restclient.el]], but +better+ with ~org-mode~
#+BEGIN_SRC emacs-lisp
  (use-package verb
    :config
    (my-local-leader
      :keymaps 'verb-mode-map
      :state 'normal
      "m" 'verb-send-request-on-point
      "vv" 'verb-set-var))
#+END_SRC
*** Set local variables to safe
For the custom template I'm using for verb files, I set some variables to ~safe~ in order to avoid prompts everytime I open one of those files
#+BEGIN_SRC emacs-lisp
  (add-to-list 'safe-local-variable-values '(flyspell-mode))
  (add-to-list 'safe-local-variable-values '(org-fontify-emphasized-text))
#+END_SRC
** Persistent scratch
#+BEGIN_SRC emacs-lisp
  (use-package persistent-scratch
    :config
    (persistent-scratch-setup-default)
    (add-hook 'kill-emacs-hook 'persistent-scratch-save))
#+END_SRC
** Perspective
#+BEGIN_SRC emacs-lisp
  (use-package perspective
    :custom
    (persp-modestring-short t)
    (persp-sort 'access)
    :config
    (my-leader
      :infix "TAB"
      "" '(:ignore t :which-key "Persp")
      "TAB" '(persp-switch-last :which-key "Switch to last persp")
      "n" '(persp-switch :which-key "Switch/create to persp")
      "k" '(persp-kill :which-key "Kill persp")
      "r" '(persp-rename :which-key "Rename persp"))
    (persp-mode))
#+END_SRC
*** Persp-projectile
#+BEGIN_SRC emacs-lisp
  (use-package persp-projectile)
#+END_SRC
** Ledger
#+BEGIN_SRC emacs-lisp
  (use-package ledger-mode
    :hook (ledger-mode . company-mode)
    :custom
    (ledger-complete-in-steps t)
    :config
    (setq ledger-reports '(("balance" "%(binary) -f %(ledger-file) bal")
                          ("real-balance" "%(binary) -f %(ledger-file) bal --cleared --real")
                          ("reg" "%(binary) -f %(ledger-file) reg")
                          ("payee" "%(binary) -f %(ledger-file) reg @%(payee)")
                          ("account" "%(binary) -f %(ledger-file) bal %(account)")))
    (my-local-leader
      :keymaps 'ledger-mode-map
      :state 'motion
      "a" 'ledger-add-transaction
      "b" 'ledger-display-balance-at-point
      "r" 'ledger-report)
    (add-hook 'ledger-mode-hook
              (lambda () (add-hook 'before-save-hook 'ledger-mode-clean-buffer nil 'local))))
#+END_SRC
*** Ledger Flycheck
#+BEGIN_SRC emacs-lisp
  (use-package flycheck-ledger)
#+END_SRC
** Dired
#+BEGIN_SRC emacs-lisp
  (use-package dired
    :ensure nil
    :custom (dired-listing-switches "-agho --group-directories-first")
    :config
    (my-leader
      :infix "o"
      "d" '(dired-jump :which-key "Dired")))
#+END_SRC
* Editing
** Evil
Embrace the anarchy. I love vim's modal editing. I hate vim as an editor
#+BEGIN_SRC emacs-lisp
  (use-package evil
    :requires undo-tree
    :custom
    (evil-undo-system 'undo-tree)
    :init
    (setq evil-want-integration t)
    (setq evil-want-keybinding nil)
    :config
    (evil-mode 1))
#+END_SRC
*** Evil collection
#+BEGIN_SRC emacs-lisp
  (use-package evil-collection
    :after evil
    :config
    (evil-collection-init))
#+END_SRC
*** Evil snipe
#+BEGIN_SRC emacs-lisp
  (use-package evil-snipe
    :custom
    (evil-snipe-smart-case t)
    (evil-snipe-auto-scroll t)
    :init
    (evil-snipe-mode t)
    (evil-snipe-override-mode t)
    ;; Evil-snipe conflicts with Magit
    (add-hook 'magit-mode-hook 'turn-off-evil-snipe-override-mode))
#+END_SRC
*** Evil args
#+BEGIN_SRC emacs-lisp
  (use-package evil-args
    :config
    ;; bind evil-args text objects
    (define-key evil-inner-text-objects-map "a" 'evil-inner-arg)
    (define-key evil-outer-text-objects-map "a" 'evil-outer-arg)

    ;; bind evil-forward/backward-args
    (define-key evil-normal-state-map "L" 'evil-forward-arg)
    (define-key evil-normal-state-map "H" 'evil-backward-arg)
    (define-key evil-motion-state-map "L" 'evil-forward-arg)
    (define-key evil-motion-state-map "H" 'evil-backward-arg))
#+END_SRC
*** Evil commentary
Allows to comment word-objects
#+BEGIN_SRC emacs-lisp
  (use-package evil-commentary
    :init
    (evil-commentary-mode t))

#+END_SRC
*** Evil surround
Allows to modify surroundings of word-objects
#+BEGIN_SRC emacs-lisp
  (use-package evil-surround
    :init
    (global-evil-surround-mode t))
#+END_SRC
*** Evil exchange
Allows for text objects exchanging
#+BEGIN_SRC emacs-lisp
  (use-package evil-exchange
    :config
    (evil-exchange-install))
#+END_SRC
*** Evil escape
#+BEGIN_SRC emacs-lisp
  (use-package evil-escape
    :config
    (setq-default evil-escape-key-sequence "jk")
    (evil-escape-mode t))
#+END_SRC
*** Evil indent
#+BEGIN_SRC emacs-lisp
  (use-package evil-indent-plus
    :config
    (evil-indent-plus-default-bindings))
#+END_SRC
*** Evil numbers
#+BEGIN_SRC emacs-lisp
  (use-package evil-numbers
    :config
    (define-key evil-normal-state-map (kbd "C-c +") 'evil-numbers/inc-at-pt)
    (define-key evil-normal-state-map (kbd "C-c -") 'evil-numbers/dec-at-pt)
    (define-key evil-visual-state-map (kbd "C-c +") 'evil-numbers/inc-at-pt)
    (define-key evil-visual-state-map (kbd "C-c -") 'evil-numbers/dec-at-pt))
#+END_SRC
*** Evil multiple cursors
#+BEGIN_SRC emacs-lisp
  (use-package evil-mc
    :hook ((prog-mode text-mode) . evil-mc-mode)
    :config
    (defhydra hydra-mc (:color red)
      "Multiple cursors"
      ("n" evil-mc-make-and-goto-next-match "Create and next match")
      ("p" evil-mc-make-and-goto-prev-match "Create and previous match")
      ("N" evil-mc-skip-and-goto-next-match "Skip to next match")
      ("P" evil-mc-skip-and-goto-prev-match "Skip to previous match")
      ("m" evil-mc-make-all-cursors "Create all cursors")
      ("j" evil-mc-make-cursor-move-next-line "Create and next line")
      ("k" evil-mc-make-cursor-move-prev-line "Create and previous line")
      ("q" evil-mc-undo-all-cursors "Undo all cursors" :color blue))
    (general-define-key
     :states '(normal visual)
     :keymaps 'override
     "gr" 'hydra-mc/body))
#+END_SRC
** Electric parenthesis
#+BEGIN_SRC emacs-lisp
  (electric-pair-mode t)
#+END_SRC
** Remove whitespace
This removes whitespace prior to saving
#+BEGIN_SRC emacs-lisp
  (add-hook 'before-save-hook 'delete-trailing-whitespace)
#+END_SRC
** Flycheck
Syntax checker and linter on the fly
#+BEGIN_SRC emacs-lisp
  (use-package flycheck
    :init (global-flycheck-mode))

#+END_SRC
* Completion
** LSP
#+BEGIN_SRC emacs-lisp
  (use-package lsp-mode
    :hook ((lsp-mode . lsp-enable-which-key-integration))
    :custom
    (lsp-headerline-breadcrumb-enable nil)
    (lsp-enable-symbol-highlighting t)
    (lsp-lens-enable t)
    (lsp-eldoc-enable-hover t)
    (lsp-modeline-diagnostics-enable t)
    (lsp-diagnostics-provider 'flycheck)
    :commands lsp
    :config
    (my-leader
      :infix "c"
      "" '(:ignore t :which-key "Code")
      "d" '(lsp-find-definition :which-key "Find definition")
      "D" '(lsp-find-references :which-key "Find references")
      "r" '(lsp-rename :which-key "Rename symbol")))

  (use-package lsp-ui
    :custom
    (lsp-ui-doc-position 'bottom)
    (lsp-ui-sideline-enable t)
    (lsp-ui-peek-always-show t)
    :config
    (general-define-key
     :states 'normal
     "gd" 'lsp-find-definition
     "gD" 'lsp-ui-peek-find-references)
    (general-define-key
     :keymaps 'lsp-ui-peek-mode-map
     "C-j" 'lsp-ui-peek--select-next
     "C-k" 'lsp-ui-peek--select-prev))

  (use-package lsp-ivy :commands lsp-ivy-workspace-symbol)
#+END_SRC
** Company
My choice for auto-completion
#+BEGIN_SRC emacs-lisp
  (use-package company
    :ensure t
    :after lsp-mode
    :hook (prog-mode . company-mode)
    :custom
    (company-begin-commands '(self-insert-command))
    (company-idle-delay 0.0)
    (company-minimum-prefix-length 1)
    (company-show-numbers t)
    (company--dabbrev-code-everywhere t)
    (company-dabbrev-downcase nil)
    (company-dabbrev-ignore-case t)
    (company-tooltip-align-annotations t)
    (company-show-quick-access t)
    (company-frontends
     '(company-tng-frontend
       company-pseudo-tooltip-frontend
       company-echo-metadata-frontend))
    :config
    (company-tng-configure-default))
#+END_SRC
** Company box
#+BEGIN_SRC emacs-lisp
  (use-package company-box
    :hook (company-mode . company-box-mode))
#+END_SRC
** YASnippet
#+BEGIN_SRC emacs-lisp
  (use-package yasnippet
    :custom (yas-snippet-dirs '("~/.emacs.d/snippets"))
    :hook (yas-before-expand-snippet . evil-insert-state)
    :init
    (yas-global-mode 1))
#+END_SRC
** File templates
Idea taken from [[http://howardism.org/Technical/Emacs/templates-tutorial.html][here]]
#+BEGIN_SRC emacs-lisp
  (use-package autoinsert
    :custom
    (auto-insert-query nil)
    (auto-insert-directory "~/.emacs.d/templates")
    :hook (find-file . auto-insert)
    :config
    (defun autoinsert-yas-expand()
      "Replace text in yasnippet template."
      (yas-expand-snippet (buffer-string) (point-min) (point-max)))
    (define-auto-insert "\\.py?$" ["default-py.py" autoinsert-yas-expand])
    (define-auto-insert "\\endpoints.org?$" ["default-endpoints.org" autoinsert-yas-expand])
    (auto-insert-mode t))
#+END_SRC
* UI
** Doom-theme
I like how Doom looks, but it's way too convoluted for my taste, so I just grab their theme
#+BEGIN_SRC emacs-lisp
  (use-package doom-themes
    :config
    (load-theme 'doom-one t)
    (doom-themes-org-config))
#+END_SRC

** All the icons
We take advantage of running Emacs as a GUI, and get nice icons for it
#+BEGIN_SRC emacs-lisp
  (use-package all-the-icons)
#+END_SRC
** Doom-modeline
Nice replacement for default mode line
#+BEGIN_SRC emacs-lisp
  (use-package doom-modeline
    :hook (after-init . doom-modeline-mode)
    :defer t
    :custom
    (doom-modeline-buffer-file-name-style 'auto)
    :config
    (set-face-attribute 'doom-modeline-evil-normal-state nil :foreground "skyblue2")
    (set-face-attribute 'doom-modeline-evil-insert-state nil :foreground "green"))
#+END_SRC

** Cursor colors
Adds visual aid to the modeline to know which mode I'm in
#+BEGIN_SRC emacs-lisp
  (setq evil-emacs-state-cursor '("red" bar))
  (setq evil-normal-state-cursor '("skyblue2" box))
  (setq evil-visual-state-cursor '("gray" box))
  (setq evil-insert-state-cursor '("green" bar))
  (setq evil-replace-state-cursor '("red" hollow))
  (setq evil-operator-state-cursor '("red" hollow))
#+END_SRC

** Rainbow-delimiters
Visual aid to know which parenthesis is paired to which
#+BEGIN_SRC emacs-lisp
  (use-package rainbow-delimiters
    :hook (prog-mode . rainbow-delimiters-mode))
#+END_SRC

** Show-paren
Highlight matching parenthesis on selection
#+BEGIN_SRC emacs-lisp
  (show-paren-mode t)
#+END_SRC
** Indent guides
Visual aid for indentation
#+BEGIN_SRC emacs-lisp
  (use-package highlight-indent-guides
    :config
    (setq highlight-indent-guides-responsive 'top)
    (setq highlight-indent-guides-method 'character)
    (add-hook 'prog-mode-hook 'highlight-indent-guides-mode))
#+END_SRC

** Line highlight
Highlights current line to aid with quick cursor finding
#+BEGIN_SRC emacs-lisp
  (global-hl-line-mode t)
#+END_SRC
** Pretty symbols
In emacs 24.4 we got prettify-symbols-mode which replaces things like lambda with λ. This can help make the code easier to read. The inhibit-compacting-font-caches stops garbage collect from trying to handle font caches which makes things a lot faster and saves us ram.
#+BEGIN_SRC emacs-lisp
  (setq prettify-symbols-unprettify-at-point 'right-edge)
  (setq inhibit-compacting-font-caches t)
#+END_SRC
*** Global
These symbols are the basics I like enabled for all ~prog-mode~ modes.
#+BEGIN_SRC emacs-lisp
  (add-hook 'prog-mode-hook
            (lambda ()
              (push '("!=" . ?≠) prettify-symbols-alist)
              (push '("<=" . ?≤) prettify-symbols-alist)
              (push '(">=" . ?≥) prettify-symbols-alist)
              (push '("=>" . ?⇒) prettify-symbols-alist)))
#+END_SRC
*** Python
#+BEGIN_SRC emacs-lisp
  (add-hook 'python-mode-hook
            (lambda ()
              (push '("def"    . ?ƒ) prettify-symbols-alist)
              (push '("sum"    . ?Σ) prettify-symbols-alist)
              (push '("**2"    . ?²) prettify-symbols-alist)
              (push '("**3"    . ?³) prettify-symbols-alist)
              (push '("None"   . ?∅) prettify-symbols-alist)
              (push '("in"     . ?∈) prettify-symbols-alist)
              (push '("not in" . ?∉) prettify-symbols-alist)
              (push '("return" . ?➡) prettify-symbols-alist)
              (prettify-symbols-mode t)))
#+END_SRC
** Nyan mode
#+BEGIN_SRC emacs-lisp
  (use-package nyan-mode
    :config
    (nyan-mode)
    (nyan-start-animation))
#+END_SRC
** Visual fill column
#+BEGIN_SRC emacs-lisp
  (use-package visual-fill-column
    :defer t
    :custom
    (visual-fill-column-width 110)
    (visual-fill-column-center-text t))
#+END_SRC
** SVG-Tags
#+BEGIN_SRC emacs-lisp
  (use-package svg-tag-mode
    :init
    (add-hook 'python-mode-hook (lambda ()
                                  (setq svg-tag-tags '(
                                                       ("# TODO[([:alpha:][:blank:])]*[\b:]*" .
                                                        ((lambda (tag) (svg-tag-make tag
                                                                                     :face 'org-todo
                                                                                     :inverse t
                                                                                     :crop-right t
                                                                                     :padding 0
                                                                                     :beg 2
                                                                                     :end -1
                                                                                     :alignment 1))))
                                                       ("# TODO[([:alpha:][:blank:])]*[\b:]*\\(.*\\)" .
                                                        ((lambda (tag) (svg-tag-make tag :face 'org-todo :crop-left t))))))
                                  (svg-tag-mode t)))
    (add-hook 'org-mode-hook (lambda ()
                               (setq svg-tag-tags '(
                                                    ;; Org tags
                                                    (":\\([A-Za-z0-9-_]+\\)" . ((lambda (tag) (svg-tag-make tag :face 'org-tag))))

                                                    ;; TODO / DONE
                                                    ("TODO" . ((lambda (tag) (svg-tag-make "TODO" :face 'org-todo :inverse t :margin 0))))
                                                    ("DONE" . ((lambda (tag) (svg-tag-make "DONE" :face 'org-done :margin 0))))))
                               (svg-tag-mode t))))
#+END_SRC

* Org
** Basic config
#+BEGIN_SRC emacs-lisp
  (defun aropie/org-mode-setup ()
    (org-indent-mode t)
    (visual-line-mode t)
    (visual-fill-column-mode t)
    (setq evil-auto-indent nil))

  (use-package org
    :defer t
    :hook (org-mode . aropie/org-mode-setup)
    :custom
    (org-src-window-setup 'current-window)
    (org-log-done 'time)
    (org-log-into-drawer t)
    (org-agenda-start-with-log-time t)
    (org-enforce-todo-dependencies t)
    (org-hide-emphasis-markers t)
    (org-confirm-babel-evaluate nil)
    (org-ellipsis " ▾")
    (org-src-fontify-natively t)
    (org-fontify-quote-and-verse-blocks t)
    (org-src-tab-acts-natively t)
    (org-src-preserve-indentation nil)
    (org-startup-folded t)
    (org-cycle-separator-lines 2)
    :config
    (add-to-list 'org-export-backends 'md)
    (add-to-list 'org-export-backends 'beamer)

    (my-local-leader
      :keymaps 'org-mode-map
      :state 'normal
      "t" 'counsel-org-tag)

    (general-define-key
     :states 'normal
     :keymaps 'org-mode-map
     "TAB" 'org-cycle))
#+END_SRC
** Org Roam
#+BEGIN_SRC emacs-lisp
  (use-package org-roam
    :custom
    (org-roam-directory "~/RoamNotes")
    :init (setq org-roam-v2-ack t)
    :config
    (org-roam-db-autosync-mode)
    (my-leader
      :infix "o"
      "n" '(org-roam-node-find :which-key "Notes")))
#+END_SRC
** Agenda
#+BEGIN_SRC emacs-lisp
  (setq org-agenda-files '("~/Org"))
#+END_SRC
** Org bullets
#+BEGIN_SRC emacs-lisp
  (use-package org-bullets
    :hook (org-mode . org-bullets-mode))
#+END_SRC
** Org-pomodoro
#+BEGIN_SRC emacs-lisp
  (use-package org-pomodoro
    :defer t
    :config
    (setq org-pomodoro-ticking-sound-p t)
    (setq org-pomodoro-ticking-sound-states '(:pomodoro)))
#+END_SRC
** Org-capture
#+BEGIN_SRC emacs-lisp
  (global-set-key (kbd "C-c c") 'org-capture)
  (setq org-default-notes-file "~/Org/refile.org")
#+END_SRC
** Refile
#+BEGIN_SRC emacs-lisp
                                          ; Targets include this file and any file contributing to the agenda - up to 9 levels deep
  (setq org-refile-targets (quote ((nil :maxlevel . 9)
                                   (org-agenda-files :maxlevel . 9))))
                                          ; Use full outline paths for refile targets - we file directly with IDO
  (setq org-refile-use-outline-path t)

                                          ; Targets complete directly with IDO
  (setq org-outline-path-complete-in-steps nil)

                                          ; Allow refile to create parent tasks with confirmation
  (setq org-refile-allow-creating-parent-nodes (quote confirm))
#+END_SRC
** Org fancy priorities
#+BEGIN_SRC emacs-lisp
  (use-package org-fancy-priorities
    :hook
    (org-mode . org-fancy-priorities-mode)
    :config
    (setq org-fancy-priorities-list '((?A . "❗")
                                      (?B . "⬆")
                                      (?C . "⬇")
                                      (?D . "☕")
                                      (?1 . "⚡")
                                      (?2 . "⮬")
                                      (?3 . "⮮")
                                      (?4 . "☕")
                                      (?I . "Important"))))
#+END_SRC
* Git
#+BEGIN_SRC emacs-lisp
  (my-leader
    :infix "g"
    "" '(:ignore t :which-key "Git")
    "g" '(magit-status :which-key "Status")
    "m" '(magit-dispatch-popup :which-key "Menu")
    "c" '(magit-clone :which-key "Clone")
    "b" '(magit-branch :which-key "Branch")
    "B" '(magit-blame :which-key "Blame")
    "l" '(magit-log :which-key "Log")
    "F" '(magit-pull :which-key "Pull")
    "t" '(git-timemachine :which-key "Travel through time"))
#+END_SRC
** Magit
Git porcelain inside Emacs. Git turned into loving hugs and kisses
#+BEGIN_SRC emacs-lisp
  (use-package magit
    :custom
    (transient-default-level 5)
    (magit-diff-refine-hunk t "Show granular diffs in selected hunk")
    ;; Don't display parent/related refs in commit buffers; they are rarely
    ;; helpful and only add to runtime costs.
    (magit-revision-insert-related-refs nil)
    :hook (git-commit-mode . evil-insert-state)
    :config
    (magit-add-section-hook 'magit-status-sections-hook 'magit-insert-assume-unchanged-files 'magit-insert-stashes))
#+END_SRC
** Timemachine
Take your code for a travel through time (that is incidentally,
highly dependant on your commits)
#+BEGIN_SRC emacs-lisp
  (use-package git-timemachine
    :after hydra
    :config
    (defhydra hydra-timemachine (:color pink)
      "Time machine"
      ("n" git-timemachine-show-next-revision "next")
      ("p" git-timemachine-show-previous-revision "previous")
      ("c" git-timemachine-show-current-revision "current")
      ("b" git-timemachine-blame "blame")
      ("s" git-timemachine-switch-branch "switch branch")
      ("q" (kill-matching-buffers "timemachine" t t) "quit" :color blue))

    (add-hook 'git-timemachine-mode-hook
              (lambda () (hydra-timemachine/body))))
#+END_SRC
* Languages
** Python
#+BEGIN_SRC emacs-lisp
  (use-package python-mode
    :hook (python-mode . lsp-deferred)
    :config
    (setq flycheck-flake8-maximum-line-length 88)
    (add-hook 'python-mode-hook
              (lambda () (local-unset-key (kbd "<backspace>")))))
#+END_SRC
*** Language Server
#+BEGIN_SRC emacs-lisp
  (use-package lsp-pyright)
#+END_SRC
*** Isort
#+BEGIN_SRC emacs-lisp
  (defun aropie/isort-buffer()
    "Runs isort on buffer"
    (interactive)
    (if (and (executable-find "isort") (projectile-project-p))
        (let ((default-directory (projectile-project-root)))
          (shell-command (format "isort --profile=black %s"
                                 (shell-quote-argument (buffer-file-name)))))

      (warn "python-mode: Cannot find isort executable.")))
  (add-hook 'python-mode-hook
            (lambda () (add-hook 'after-save-hook 'aropie/isort-buffer nil 'local)))
#+END_SRC
*** Black
#+BEGIN_SRC emacs-lisp
  (use-package python-black
    :config
    :hook (python-mode . python-black-on-save-mode))
#+END_SRC
** JavaScript
#+BEGIN_SRC emacs-lisp
  (use-package js2-mode
    :hook
    (js-mode . lsp-deferred)
    (js-mode . js2-minor-mode)
    :custom
    (js2-highlight-level 3)
    (js-indent-level 2))
#+END_SRC
** PHP
#+BEGIN_SRC emacs-lisp
  (use-package php-mode)
#+END_SRC

** Markup
*** Yaml
#+BEGIN_SRC emacs-lisp
  (use-package yaml-mode)
#+END_SRC
** DSL
*** Jenkinsfile
#+BEGIN_SRC emacs-lisp
  (use-package jenkinsfile-mode)
#+END_SRC
*** PlantUML
#+BEGIN_SRC emacs-lisp
  (use-package plantuml-mode
    :custom
    (plantuml-default-exec-mode 'executable)
    :config
    (setq plantuml-output-type "png")
    (add-to-list 'auto-mode-alist '("\\.uml\\'" . plantuml-mode)))
#+END_SRC
* Packages to consider
- https://github.com/gilbertw1/better-jumper
- https://cestlaz.github.io/post/using-emacs-57-dired-narrow/
- https://github.com/akhramov/org-wild-notifier.el
- https://github.com/mineo/yatemplate
- https://github.com/karthink/popper
